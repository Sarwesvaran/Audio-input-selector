#include <Arduino.h>
#include <EEPROM.h>
#include <Wire.h>
#include <Adafruit_GFX.h>
#include <Adafruit_SSD1306.h>

#define sw 7
#define ENC_A 3
#define ENC_B 2

// Define 6 LED Pins
const int ledPins[] = {4, 5, 6, 9, 10, 11}; // Pins for AUX, USB, BT, OPT, WIFI, HDMI

#define SCREEN_WIDTH 128
#define SCREEN_HEIGHT 64
#define OLED_RESET -1
Adafruit_SSD1306 display(SCREEN_WIDTH, SCREEN_HEIGHT, &Wire, OLED_RESET);

int menu = 1;
int lastClk;
int sel = 0;
int menu_mem = 0;

// --- MENU ICONS (18x16) ---
const unsigned char icon_aux[] PROGMEM = {0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0xf0, 0x00, 0x00, 0xf0, 0x00, 0x00, 0xff, 0xc3, 0x00, 0xf0, 0x7c, 0x80, 0xf0, 0x44, 0x40, 0xf0, 0x44, 0x40, 0xf0, 0x7c, 0x80, 0xff, 0xc3, 0x00, 0xf0, 0x00, 0x00, 0xf0, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00};
const unsigned char icon_bt[] PROGMEM = {0x00, 0xc0, 0x00, 0x00, 0xa0, 0x00, 0x00, 0x90, 0x00, 0x00, 0x88, 0x00, 0x08, 0x90, 0x00, 0x04, 0xa0, 0x00, 0x02, 0xc0, 0x00, 0x01, 0x80, 0x00, 0x02, 0xc0, 0x00, 0x04, 0xa0, 0x00, 0x08, 0x90, 0x00, 0x00, 0x88, 0x00, 0x00, 0x90, 0x00, 0x00, 0xa0, 0x00, 0x00, 0xc0, 0x00, 0x00, 0x00, 0x00};
const unsigned char icon_hdmi[] PROGMEM = {0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x7f, 0xff, 0x80, 0x40, 0x00, 0x80, 0x4f, 0xfc, 0x80, 0x20, 0x01, 0x00, 0x1f, 0xfe, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00};
const unsigned char icon_optical[] PROGMEM = {0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x07, 0xf8, 0x00, 0x0f, 0xfc, 0x00, 0x1f, 0xfe, 0x00, 0x1f, 0xfe, 0x00, 0x3f, 0xff, 0x00, 0x3f, 0x3f, 0x00, 0x3e, 0x1f, 0x00, 0x1e, 0x1e, 0x00, 0x1f, 0x3e, 0x00, 0x1f, 0xfe, 0x00, 0x1f, 0xfe, 0x00, 0x1f, 0xfe, 0x00, 0x00, 0x00, 0x00};
const unsigned char icon_usb[] PROGMEM = {0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x01, 0x80, 0x00, 0x06, 0x40, 0x00, 0x09, 0x80, 0x00, 0x10, 0x04, 0x00, 0x20, 0x06, 0x00, 0xff, 0xff, 0x00, 0x04, 0x06, 0x00, 0x04, 0x04, 0x00, 0x02, 0x60, 0x00, 0x01, 0x90, 0x00, 0x00, 0x60, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00};
const unsigned char icon_wifi[] PROGMEM = {0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x07, 0xf0, 0x00, 0x08, 0x08, 0x00, 0x10, 0x04, 0x00, 0x23, 0xe2, 0x00, 0x04, 0x10, 0x00, 0x08, 0x08, 0x00, 0x01, 0xc0, 0x00, 0x02, 0x20, 0x00, 0x00, 0x00, 0x00, 0x00, 0x80, 0x00, 0x01, 0xc0, 0x00, 0x00, 0x80, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00};

const char *modeNames[] = {"", "AUXILIARY", "USB AUDIO", "BLUETOOTH", "OPTICAL", "WI-FI", "HDMI ARC"};

void updateLEDs(int activeMode)
{
  for (int i = 0; i < 6; i++)
  {

    if (activeMode != 0 && i == (activeMode - 1))
    {
      digitalWrite(ledPins[i], LOW);
    }
    else
    {
      digitalWrite(ledPins[i], HIGH);
    }
  }
}

void drawUI(int currentMode, bool isActive)
{
  display.clearDisplay();

  const unsigned char *currentIcon;
  switch (currentMode)
  {
  case 1:
    currentIcon = icon_aux;
    break;
  case 2:
    currentIcon = icon_usb;
    break;
  case 3:
    currentIcon = icon_bt;
    break;
  case 4:
    currentIcon = icon_optical;
    break;
  case 5:
    currentIcon = icon_wifi;
    break;
  case 6:
    currentIcon = icon_hdmi;
    break;
  }

  display.drawBitmap(55, 10, currentIcon, 18, 16, WHITE);

  display.setTextSize(1);
  display.setTextColor(WHITE);
  int16_t x1, y1;
  uint16_t w, h;
  display.getTextBounds(modeNames[currentMode], 0, 0, &x1, &y1, &w, &h);
  display.setCursor((128 - w) / 2, 35);
  display.print(modeNames[currentMode]);

  if (isActive)
  {
    display.drawRoundRect(5, 5, 118, 54, 4, WHITE);
    display.setCursor(45, 50);
    display.print("ACTIVE");
  }
  else
  {
    display.setCursor(35, 50);
    display.print("< SELECT >");
  }
  display.display();
}

void setup()
{
  display.begin(SSD1306_SWITCHCAPVCC, 0x3C);
  display.clearDisplay();

  pinMode(sw, INPUT_PULLUP);
  pinMode(ENC_A, INPUT);
  pinMode(ENC_B, INPUT);

  for (int i = 0; i < 6; i++)
  {
    pinMode(ledPins[i], OUTPUT);
    digitalWrite(ledPins[i], HIGH);
  }

  lastClk = digitalRead(ENC_A);
  menu = EEPROM.read(menu_mem);
  if (menu < 1 || menu > 6)
    menu = 1;

  sel = 1;
  updateLEDs(menu);
  drawUI(menu, true);
}

void loop()
{
  int currentClk = digitalRead(ENC_A);
  if (currentClk != lastClk && currentClk == LOW)
  {
    if (digitalRead(ENC_B) != currentClk)
      menu++;
    else
      menu--;

    if (menu > 6)
      menu = 1;
    if (menu < 1)
      menu = 6;

    sel = 0;
    updateLEDs(0);
    drawUI(menu, false);
  }
  lastClk = currentClk;

  if (digitalRead(sw) == LOW && sel == 0)
  {
    delay(50);
    EEPROM.write(menu_mem, menu);
    sel = 1;
    updateLEDs(menu);
    drawUI(menu, true);
    delay(200);
  }
}